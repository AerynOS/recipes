build_profile := env_var_or_default('BOULDER_PROFILE', 'local-x86_64')
build_file := join(invocation_directory(), "stone.yaml")

pkgs_file := join(invocation_directory(), "pkglist")
cache_dir := join(invocation_directory(), "moss_cache")
blit_dir := join(invocation_directory(), "root")

default: build

# Prepare the ephemeral root + storage for kernel work
_prepare_root:
    #!/usr/bin/env bash
    set -euxo pipefail
    if [[ ! -d "{{cache_dir}}" ]]; then
        mkdir "{{cache_dir}}"
        moss -D {{cache_dir}} repo add volatile "https://packages.serpentos.com/volatile/x86_64/stone.index"
    else
        moss -D {{cache_dir}} repo update
    fi
    if [[ -d "{{blit_dir}}" ]]; then
        sudo rm -rf "{{blit_dir}}"
    fi
    mkdir "{{blit_dir}}"
    moss -D "{{cache_dir}}" install --to="{{blit_dir}}" $(cat {{pkgs_file}}) --yes-all

# Enter nspawn environment for kernel updates
nspawn: _prepare_root
    sudo systemd-nspawn -D "{{blit_dir}}" --bind=$(pwd):/kernel --chdir=/kernel


# Build the stone.yml recipe using boulder
_build target:
    cd {{ invocation_directory() }}; boulder build -u {{ if path_exists(target) == "true" { target } else { error("Missing stone.yml file") } }} -p {{ build_profile }}

# Build stone.yml from the current directory
build: (_build build_file)
